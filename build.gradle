buildscript {
	dependencies { classpath 'com.bancvue:gradle-internal:2.+' }
}

apply plugin: 'bancvue-internal'
apply plugin: 'ci'


configurations {
	restCompile.extendsFrom(compile)
	restRuntime.extendsFrom(runtime)

	all*.exclude module: 'slf4j-simple'
}

sourceSets {
	rest {
		compileClasspath = main.output + configurations.restCompile
		runtimeClasspath = rest.output + main.output + configurations.restRuntime

		java {
			srcDir "src/main/java"
			srcDir "src/rest/java"
		}
	}
	test {
		compileClasspath += rest.output + rest.compileClasspath
		runtimeClasspath += rest.output + rest.runtimeClasspath
	}
	componentTest {
		compileClasspath += rest.output + rest.compileClasspath
		runtimeClasspath += rest.output + rest.runtimeClasspath
	}
}


String springBootVersion = '1.0.2.RELEASE'
String jerseyVersion = '2.7'

dependencies {
	compile "org.springframework.boot:spring-boot:${springBootVersion}"
	compile "org.springframework.boot:spring-boot-starter:${springBootVersion}"
	compile "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
	compile 'com.bancvue:configuration-management:1.+'
	compile 'org.liquibase:liquibase-core:3.1.1'
	// Fix liquibase logging
	compile 'com.mattbertolini:liquibase-slf4j:1.2.1'
	compile 'org.projectlombok:lombok:1.12.2'
	compile('net.sf.dozer:dozer:5.4.0') {
		exclude group: 'org.slf4j'
	}

	restCompile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
	restCompile "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
	restCompile "org.glassfish.jersey.core:jersey-server:${jerseyVersion}"
	restCompile("org.glassfish.jersey.ext:jersey-spring3:${jerseyVersion}") {
		exclude group: "junit"
	}
	restCompile "org.glassfish.jersey.ext:jersey-bean-validation:${jerseyVersion}"
	restCompile "org.hibernate:hibernate-validator:5.0.2.Final"
	restCompile "com.bancvue:common-rest:1.+"

	mainTestCompile 'org.codehaus.groovy:groovy-all:2.1.2'
	mainTestCompile 'org.glassfish.jersey.core:jersey-server:2.7'
	mainTestCompile "org.springframework:spring-test:4.0.3.RELEASE"
	mainTestCompile 'org.fluttercode.datafactory:datafactory:0.8'

	testCompile 'org.codehaus.groovy:groovy-all:2.1.2'
	testCompile 'cglib:cglib-nodep:2.2.2'
	testCompile 'org.objenesis:objenesis:1.3'
	testCompile('org.spockframework:spock-unitils:0.7-groovy-2.0') {
		exclude group: 'org.codehaus.groovy'
	}
	testCompile("org.spockframework:spock-spring:0.7-groovy-2.0") {
		exclude group: "org.codehaus.groovy"
	}
}

idea {
	module {
		scopes.COMPILE.plus += configurations.restCompile
	}
}

publishing_ext {
	publication('rest')
	publication('mainTest')
}