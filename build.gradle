buildscript {
	dependencies { classpath 'com.bancvue:gradle-internal:2.+' }
}

apply plugin: 'bancvue-internal'
apply plugin: 'ci'


configurations {
	restCompile.extendsFrom(compile)
	restRuntime.extendsFrom(runtime)
	restClientCompile
	restClientRuntime

	all*.exclude module: 'slf4j-simple'
}

sourceSets {
	rest {
		compileClasspath = main.output + configurations.restCompile
		runtimeClasspath = rest.output + main.output + configurations.restRuntime

		java {
			srcDir "src/main/java"
			srcDir "src/rest/java"
		}
	}
	restClient {
		compileClasspath = configurations.restClientCompile
		runtimeClasspath = restClient.output + configurations.restClientRuntime

		java {
			srcDir "src/main/java"
			srcDir "src/rest/java"
			include "com/bancvue/boot/api/**"
			include "com/bancvue/boot/client/**"
			include "com/bancvue/boot/jackson/mapper/**"
		}
	}
	test {
		compileClasspath += rest.output + rest.compileClasspath
		runtimeClasspath += rest.output + rest.runtimeClasspath
	}
	componentTest {
		compileClasspath += rest.output + rest.compileClasspath
		runtimeClasspath += rest.output + rest.runtimeClasspath
	}
}


String springBootVersion = '1.1.4.RELEASE'
String jerseyVersion = '2.7'

dependencies {
	compile "org.springframework.boot:spring-boot:${springBootVersion}"
	compile "org.springframework.boot:spring-boot-starter:${springBootVersion}"
	compile "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
	compile "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
	compile 'com.ryantenney.metrics:metrics-spring:3.0.0'
	compile 'com.bancvue:common-config:2.+'
	compile 'org.codehaus.janino:janino:2.6.1'
	compile 'org.liquibase:liquibase-core:3.1.1-bv.1.0'
	// Fix liquibase logging
	compile 'com.mattbertolini:liquibase-slf4j:1.2.1'
	compile 'org.projectlombok:lombok:1.12.2'
	compile('net.sf.dozer:dozer:5.4.0-bv.1.0') {
		exclude group: 'org.slf4j'
	}

	restCompile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
	restCompile "org.glassfish.jersey.core:jersey-server:${jerseyVersion}"
	restCompile("org.glassfish.jersey.ext:jersey-spring3:${jerseyVersion}") {
		exclude group: "junit"
	}
	restCompile "org.glassfish.jersey.ext:jersey-bean-validation:${jerseyVersion}"
	restCompile "org.hibernate:hibernate-validator:5.0.2.Final"
	restCompile "com.bancvue:common-rest:1.+"
	restCompile "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.3.3"
	restCompile "joda-time:joda-time:2.3"

	restClientCompile "com.bancvue:common-rest:1.+"
	restClientCompile "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.3.3"
	restClientCompile "joda-time:joda-time:2.3"

	mainTestCompile 'org.codehaus.groovy:groovy-all:2.1.2'
	mainTestCompile 'org.glassfish.jersey.core:jersey-server:2.7'
	mainTestCompile "org.springframework:spring-test:4.0.3.RELEASE"
	mainTestCompile 'org.fluttercode.datafactory:datafactory:0.8'
	mainTestCompile 'com.bancvue:common-config-test:2.+'

	mainTestCompile 'cglib:cglib-nodep:2.2.2'
	mainTestCompile 'org.objenesis:objenesis:1.3'
	mainTestCompile("org.spockframework:spock-spring:0.7-groovy-2.0") {
		exclude group: "org.codehaus.groovy"
	}
	mainTestCompile('org.spockframework:spock-unitils:0.7-groovy-2.0') {
		exclude group: 'org.codehaus.groovy'
	}
}

idea {
	module {
		sourceDirs += file('src/rest/java')
		scopes.COMPILE.plus += configurations.restCompile
	}
}

publishing_ext {
	publication('rest')
	publication('restClient')
	publication('mainTest')
}